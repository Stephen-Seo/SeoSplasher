
apply plugin: 'cpp'

if(hasProperty('SFML2_DIR') && hasProperty('EXTLIBS_DIR'))
{
    model {
        buildTypes {
            debug
            release
        }

        platforms {
            windows_x86 {
                operatingSystem "windows"
                architecture "x86"
            }
        }

        toolChains {
            mingw_x86(Gcc) {
                path "/usr/bin"
                eachPlatform() {
                    cCompiler.executable "i686-w64-mingw32-gcc"
                    cppCompiler.executable "i686-w64-mingw32-g++"
                    linker.executable "i686-w64-mingw32-g++"
                    assembler.executable "i686-w64-mingw32-as"
                    staticLibArchiver.executable "i686-w64-mingw32-ar"
                }
                target("windows_x86")
            }
        }
    }

    executables {
        SeoSplasher {
            targetPlatforms "windows_x86"

            binaries.all {
                cppCompiler.args "-std=c++11", "-Wall", "-Wextra"
                if(project.hasProperty('SET_RPATH'))
                {
                    linker.args "-Xlinker", "-rpath=${SET_RPATH}"
                }

                if(project.hasProperty('ResourcePacker_DIR')) {
                    cppCompiler.define "ResourcePacker_FOUND"
                }

                if(buildType == buildTypes.debug) {
                    cppCompiler.args "-O0", "-g"
                }
                if(buildType == buildTypes.release) {
                    cppCompiler.define "NDEBUG"
                    cppCompiler.args "-O3"
                }
            }
        }
    }

    sources {
        SeoSplasher {
            cpp {
                if(project.hasProperty('ResourcePacker_DIR')) {
                    lib project: ":RP_windows_x86", library: "ResourcePacker"
                }
                lib project: ":sfml_windows_x86", library: "sfml_system"
                lib project: ":sfml_windows_x86", library: "sfml_window"
                lib project: ":sfml_windows_x86", library: "sfml_network"
                lib project: ":sfml_windows_x86", library: "sfml_graphics"
                lib project: ":sfml_windows_x86", library: "sfml_audio"
                source {
                    srcDir "${rootDir}/src"
                    include "**/*.cpp"
                    exclude "ec/nMove.cpp"
                }
                exportedHeaders {
                    srcDir "${rootDir}/src"
                    srcDir "${SFML2_DIR}/src/SFML2/headers"
                }
            }
        }
    }
}

